# 関数

入力した値に対して戻り値を返す仕組みを関数といいます。

## 定義

関数は ```function``` と ```end``` で囲んで定義します。以下が基本形です。

```Julia
function func(args...)
    ...
    return ret
end
```

戻り値は ```return``` で返します。関数の途中に ```return``` がある場合はその後から関数末尾までの処理は実行されません。

以下は2つの引数の和を返す関数の例です。

```Julia
julia> function add(x, y)
           return x + y
       end
add (generic function with 1 method)
```

Julia の関数の面白い特徴として、```return``` を省略すると最後に評価された式の値が返ります。従って、上の関数は以下のように記述することも可能です。

```Julia
julia> function add(x, y)
           x + y
       end
add (generic function with 1 method)
```

このように一行で記述できるような関数の場合、さらに簡略化した代入形式（assignment form）を利用することもできます。

```Julia
julia> add(x,y) = x + y
add (generic function with 1 method)
```

## 呼び出し

関数を実際に利用する呼び出しは以下のような形で行います。

```Julia
func(args...)
```

さきほどの add 関数を利用する例を示します。

```Julia
julia> add(2, 3)
5
```

このように、入力した値に対して答えを返すのが関数の役割といえます。

## 値の受け渡し

Julia における関数の引数は、共有渡し（pass-by-sharing）で渡されます。大ざっぱには、引数の型の種類に応じて以下のように動作します。
- 数値、文字列など：関数内における変更は関数の呼び出し元に反映されない
- 配列など：関数内における変更は関数の呼び出し元に反映される

具体的な例でみていきましょう。

```Julia
julia> function addv(x, y)
         x = x + y
       end
addv (generic function with 1 method)
```

```Julia
julia> p=2
       addv(p, 5)
       p
2
```

このように、関数内で引数の値を変更しても、呼び出し元には反映されません。

一方、配列を引数として渡すと、関数内での変更が呼び出し元にも反映されます。

```Julia
julia> function addvarray!(x, y)
         x[1] = x[1] + y
         x[2] = x[2] + y
         x[3] = x[3] + y
       end
addvarray! (generic function with 1 method)
```

```Julia
julia> a=[1,2,3]
       addvarray!(a,3)
       a[1]
4
```

## 無名関数（anonymous functions）

後述の理由により、名前のない関数を定義することがあります。無名関数と呼ばれるこのような関数は、

```Julia
function (args...)
    ...
end
```

のように関数名を省略する、もしくはさらに省略して、

```Julia
args -> ...
```

のような文法で定義できます。

無名関数は、関数の引数として関数を渡す場合によく用いられます。以下は、1, 3, -1 それぞれを引数とする無名関数の結果を得る例です。

```Julia
julia> map(x -> x^2 + 2x - 1, [1, 3, -1])
3-element Vector{Int64}:
  2
 14
 -2
```

## doブロック構文

関数の引数として関数を渡す方法は有用ですが、無名関数の定義が長い場合は引数の部分が冗長になってしまいます。このような場合、以下のdoブロック構文で書き換えるとより分かりやすい記述になる場合があります。

```Julia
func( args-for-func-except-1st-arg ) do args-for-anonymous-func
  ...
end
```

たとえば、前述の

```Julia
map(x -> x^2 + 2x - 1, [1, 3, -1])
```
は、

```Julia
map([1, 3, -1]) do x
    return x^2 + 2x - 1
end
```
のような形で書き換えることが可能です。
